import com.vanniktech.maven.publish.SonatypeHost

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.vanniktech.maven.publish'

ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4]
ext.universalBase = 8000

version = "0.1.8"

android {
    namespace 'is.xyz.mpv'
    compileSdk 35

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        versionCode 38
        versionName version
    }

    buildFeatures {
        buildConfig = true
    }

    // https://youtrack.jetbrains.com/issue/KT-55947
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.7.0'
}

def getVersionName = { ->
    return version
}

def getGroupId = { ->
    return "io.github.abdallahmehiz"
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    mavenPublishing {
        publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
        signAllPublications()
        coordinates(
                getGroupId(),
                "mpv-android-lib",
                getVersionName()
        )

        pom {
            name = "mpv Android library"
            description = "The mpv library used by mpvKt."
            inceptionYear = "2024"
            url = "https://github.com/abdallahmehiz/mpv-android/"
            licenses {
                license {
                    name = "MIT License"
                    url = "https://opensource.org/license/mit/"
                    distribution = "repo"
                }
            }
            developers {
                developer {
                    id = "abdallahmehiz"
                    name = "Abdallah Mehiz"
                    url = "https://github.com/abdallahmehiz/"
                }
            }
            scm {
                url = "https://github.com/abdallahmehiz/mpv-android/"
                connection = "scm:git:git://github.com/abdallahmehiz/mpv-android.git"
                developerConnection = "scm:git:ssh://git@github.com/abdallahmehiz/mpv-android.git"
            }
        }
    }
}
